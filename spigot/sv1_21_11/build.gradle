plugins {
  alias(libs.plugins.shadow)
}

sourceCompatibility = 21
targetCompatibility = 21

configurations {
  shadowImpl
  compileOnly.extendsFrom shadowImpl
  implementation.extendsFrom shadowImpl2
}

dependencies {
  compileOnly 'org.spigotmc:spigot-api:1.21.11-R0.1-SNAPSHOT'
  //compileOnly 'io.papermc.paper:paper-api:1.21.5-R0.1-SNAPSHOT'
  shadowImpl 'net.coobird:thumbnailator:0.4.20'
  shadowImpl 'com.google.zxing:core:3.5.3'
  shadowImpl 'com.google.zxing:javase:3.5.3'
  shadowImpl 'net.kyori:adventure-api:4.24.0'
  shadowImpl 'net.kyori:adventure-platform-bukkit:4.4.1'
  
  // AWS SDK & Jackson (Package with plugin)
  shadowImpl platform('software.amazon.awssdk:bom:2.21.0')
  shadowImpl 'software.amazon.awssdk:s3'
  shadowImpl 'software.amazon.awssdk:auth'
  shadowImpl 'software.amazon.awssdk:url-connection-client'
  shadowImpl 'com.fasterxml.jackson.core:jackson-databind:2.18.3'
  shadowImpl 'com.fasterxml.jackson.core:jackson-core:2.18.3'
  shadowImpl 'com.fasterxml.jackson.core:jackson-annotations:2.18.3'

  compileOnly project(':spigot:svcore')
  shadowImpl2 project(':common')
}

build {
  dependsOn(':common:build')
  dependsOn(':spigot:svcore:build')
}

shadowJar {
  configurations = [project.configurations.shadowImpl, project.configurations.shadowImpl2]
  archiveBaseName.set('Kishax-Spigot')
  archiveClassifier.set('')
  archiveVersion.set('1.21.11')
  from(project(':spigot:svcore').sourceSets.main.output)
  exclude('META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
  
  mergeServiceFiles()

  // Relocate dependencies to avoid conflicts
  relocate 'software.amazon.awssdk', 'net.kishax.libs.aws'
  relocate 'com.fasterxml.jackson', 'net.kishax.libs.jackson'
  relocate 'org.reactivestreams', 'net.kishax.libs.reactivestreams'
}

processResources {
  from("$rootDir/spigot/src/main/resources") {
    include '**/*'
  }
  filesMatching('**/plugin.yml') {
    expand(
      project: project,
      projectName: project.name,
      projectVersion: project.version,
      projectDescription: project.property('description'),
      projectGroup: project.group,
      groupId: project.group,
      projectHP: project.property('hp'),
      projectAPIVersion: 1.21
    )
  }
}

tasks.build {
  dependsOn tasks.shadowJar
}
