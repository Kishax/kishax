plugins {
  alias(libs.plugins.shadow)
  alias(libs.plugins.neoforgemoddev)
}

java {
  modularity.inferModulePath.set(true)
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
  sourceCompatibility = 17
  targetCompatibility = 21
}

configurations {
  shadowImpl
  compileOnly.extendsFrom shadowImpl
  implementation.extendsFrom shadowImpl2
}

neoForge {
  version = '21.1.93'
  validateAccessTransformers = true

  parchment {
    mappingsVersion = '2024.11.13'
    minecraftVersion = '1.21.1'
  }

  mods {
    "kishax" {
      sourceSet(sourceSets.main)
    }
  }
}

dependencies {
  compileOnly project(':neoforge:neofovcore')
  shadowImpl2 project(':common')
  shadowImpl('com.google.code.gson:gson:2.11.0') {
    version { strictly '2.11.0' }
  }
}

build {
  dependsOn shadowJar
  dependsOn(':common:build')
  dependsOn(':neoforge:neofovcore:build')
}

shadowJar {
  configurations = [project.configurations.shadowImpl, project.configurations.shadowImpl2]
  archiveBaseName.set('Kishax-NeoForge')
  archiveClassifier.set('')
  archiveVersion.set('1.21.1')
  from(project(':neoforge:neofovcore').sourceSets.main.output)

  exclude('/mappings/*')
  exclude('META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
  relocate('com.google', 'f5.si.kishax.mc.lib.google')
  relocate('kotlin', 'f5.si.kishax.mc.lib.kotlin')
  relocate('okhttp3', 'f5.si.kishax.mc.lib.okhttp3')
  relocate('okio', 'f5.si.kishax.mc.lib.okio')
  relocate('org.aopalliance', 'f5.si.kishax.mc.lib.aopalliance')
  relocate('org.apache', 'f5.si.kishax.mc.lib.apache')
  relocate('org.checkerframework', 'f5.si.kishax.mc.lib.checkerframework')
  relocate('org.intellij', 'f5.si.kishax.mc.lib.intellij')
  relocate('org.jetbrains', 'f5.si.kishax.mc.lib.jetbrains')
  relocate('org.json', 'f5.si.kishax.mc.lib.json')
  exclude('org/slf4j/**') // using slf4j pkg by other mods' export one
  exclude('javax/annotation/**') // in the same way
  exclude('javax/annotation') // in the same way
  relocate('org.yaml', 'f5.si.kishax.mc.lib.yaml')
  relocate('redis', 'f5.si.kishax.mc.lib.redis')

  destinationDirectory.set(file("$buildDir/libs"))
}

processResources {
  from("$rootDir/neoforge/src/main/resources") {
    include '**/*'
  }
  filesMatching('META-INF/neoforge.mods.toml') {
    expand(
      project: project,
      projectName: project.name,
      projectVersion: project.version,
      projectDescription: project.property('description'),
      projectGroup: project.group,
      groupId: project.group,
      projectHP: project.property('hp'),
      projectSource: project.property('source'),
      projectIssueTracker: project.property('issue')
    )
  }
}

artifacts {
  shadow shadowJar
}
